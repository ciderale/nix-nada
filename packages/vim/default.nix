# Vim, with a set of extra packages (extraPackages) and a custom vimrc
# (./vimrc). The final vimrc file is generated by vimUtils.vimrcFile and
# bundles all the packages with the custom vimrc.
{ git, symlinkJoin, makeWrapper, vim_configurable, vimUtils, vimPlugins, haskellPackages }:
let
    myvim = vim_configurable.customize {
      name = "vim";
      vimrcConfig = {
        customRC = builtins.readFile ./vimrc;
        packages.myVimPackages.start = with vimPlugins; [
          # TODO: setup ultisnips
          # ghcmod # NOTE: the haskell package for ghc-mod is broken
          ctrlp
          fugitive
          gitgutter
          nerdcommenter
          nerdtree
          surround
          syntastic
          vim-tmux-navigator
          vim-airline
          vim-easymotion
          vim-indent-guides
          vim-markdown
          vim-multiple-cursors
          vim-nix
          vim-trailing-whitespace
          vimproc
          youcompleteme
        ];
      };
    };
in
  symlinkJoin {
    name = "vim";
    buildInputs = [makeWrapper];
    postBuild = ''
      wrapProgram "$out/bin/vim" \
      --add-flags "-X" \
      --set PATH '${haskellPackages.hasktags}/bin:${git}/bin'
      ln -s $out/bin/{vim,vi}
    '';
    paths = [ myvim ];
  }
